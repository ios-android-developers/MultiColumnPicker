apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = "1.1.1"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    resourcePrefix "multicolomn_"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 2
        versionName "1.1.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.1'
}

def siteUrl = 'https://github.com/twiceyuan/MultiColumnPicker'
def gitUrl = 'https://github.com/twiceyuan/MultiColumnPicker.git'
def projectName = 'Android Multiple Column Picker Dialog'
def licenseName = 'WTFPL'
def developerName = 'twiceyuan'
group = "com.twiceyuan.multicolumnpicker"

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name projectName
                url siteUrl

                licenses {
                    license {
                        name licenseName
                        url 'http://www.wtfpl.net/txt/copying/'
                    }
                }
                developers {
                    developer {
                        id 'twiceyuan'
                        name developerName
                        email 'twiceyuan@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'source'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "MultiColumnPicker"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["WTFPL"]
        publish = true
    }
}

apply plugin: 'me.tatarka.retrolambda'